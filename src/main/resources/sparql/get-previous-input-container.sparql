PREFIX nie: <http://www.semanticdesktop.org/ontologies/2007/01/19/nie#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX cogs: <http://vocab.deri.ie/cogs#>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX jobst: <http://redpencil.data.gift/id/concept/JobStatus/>
PREFIX task: <http://redpencil.data.gift/vocabularies/tasks/>
PREFIX tasko: <http://lblod.data.gift/id/jobs/concept/TaskOperation/>
PREFIX dlstatus: <http://lblod.data.gift/file-download-statuses/>
PREFIX prov: <http://www.w3.org/ns/prov#>

SELECT DISTINCT ?path WHERE {
  # we only want to grab the previous jobs where data were published.
  <${targetUrl}> nie:url ?derivedUrl.
  VALUES ?endOperation {
    tasko:publishHarvestedTriples
    tasko:publishHarvestedTriplesWithDeletes
  }
  ?taskPublishing
    dct:isPartOf ?job ;
    task:operation ?endOperation ;
    adms:status jobst:success ;
    dct:created ?created.

  ?taskDiff
    dct:isPartOf ?job ;
    task:operation tasko:diff ;
    adms:status jobst:success ;
    task:inputContainer ?inputContainer .

  ?inputContainer
    task:hasFile ?file .

  ?file prov:wasDerivedFrom ?derived.
  ?derived nie:url ?fromUrl.

  ?path
    nie:dataSource ?file .

  FILTER (?fromUrl = ?derivedUrl)
}
ORDER BY DESC(?created)
LIMIT 1